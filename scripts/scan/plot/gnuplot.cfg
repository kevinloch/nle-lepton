set datafile separator ","
set terminal pngcairo enhanced font 'Arial,14' size 1280,1280 background rgb "white"
set encoding utf8
set title "Coefficients and two-term test vs reference mass for geometry ".fnam
set border lc rgb 'black'
set style circle radius 0.2
set logscale xy

set multiplot

set size nosquare 0.99,0.85
set origin 0,0.15
set xrange [1.0E5:1.0E12]
set yrange [1.0E-3:1.0E3]
set arrow from 0.511E6,1.0E-3 to 0.511E6,1.0E3 nohead     # electron
set arrow from 105.66E6,1.0E-3 to 105.66E6,1.0E3 nohead   # muon
set arrow from 1776.86E6,1.0E-3 to 1776.86E6,1.0E3 nohead # tau
#set arrow from 1796.75E6,1.0E-3 to 1796.75E6,1.0E3 nohead # alpha*v
set arrow from 80.379E9,1.0E-3 to 80.379E9,1.0E3 nohead   # w
set arrow from 91.1876E9,1.0E-3 to 91.1876E9,1.0E3 nohead # z
set arrow from 125.1E9,1.0E-3 to 125.1E9,1.0E3 nohead     # Higgs
set arrow from 246.22E9,1.0E-3 to 246.22E9,1.0E3 nohead   # v
#set arrow from 6.5014E23,1.0E-3 to 6.5014E23,1.0E3 nohead # alpha^2*mp
#set arrow from 8.9093E25,1.0E-3 to 8.9093E25,1.0E3 nohead # alpha*mp
#set arrow from 1.2209E28,1.0E-3 to 1.2209E28,1.0E3 nohead # mp
set xtics add ("m_t_a_u" 1776.86E6 0)
#set xtics add ("m_u" 105.66E6 0)
set xtics add ("m_e" 0.511E6 0)
#set xtics add ("av" 1796.75E6 0)
set xtics add ("v" 246.22E9 0)
#set xtics add ("m_P" 1.2209E28 0)
set xtics rotate by -45
set xlabel 'Reference mass eV/c^2' tc rgb 'black'
set ylabel 'Coefficient value' tc rgb 'black'
plot filename using 4:6 with points lt 7 lc rgb "green" title "c_1",\
filename using 4:7 with points lt 7 lc rgb "red" title "c_2",\
filename using 4:8 with points lt 7 lc rgb "magenta" title "c_3",\
filename using 4:5 with points lt 7 lc rgb "blue" title "two-term test = c_3/(c_1*c_2)",1.0,2.0

set size nosquare 0.99,0.15
unset title
set origin 0,0
set xrange [1.0E5:1.0E12]
set yrange [1.0E3:1.0E10]
unset arrow
unset xtics
set xtics
unset xlabel
set ylabel 'Samples'
set format x ''
plot filename using 4:9 with points lt 7 lc rgb "light-blue" title "phase 1 mc samples to solve"

unset multiplot
